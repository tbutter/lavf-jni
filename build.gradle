plugins {
    id 'de.undercouch.download' version '4.1.1'
    id("maven-publish")
}
apply plugin: 'maven'
apply plugin: 'java'

group = 'com.blubb.lavf'
version = '1.0-SNAPSHOT'

repositories {
	jcenter()
}

dependencies {
	testCompile group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.7.0'
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/tbutter/lavf-jni")
            credentials {
        username = System.getenv("GITHUB_ACTOR")
        password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "PASSED", "SKIPPED", "FAILED", "STANDARD_OUT", "STANDARD_ERROR"
        exceptionFormat "full"
    }
}

task downloadFile(type: Download) {
    src 'http://commondatastorage.googleapis.com/gtv-videos-bucket/sample/ForBiggerFun.mp4'
    dest buildDir
	onlyIfModified true
}

task verifyFile(type: Verify, dependsOn: downloadFile) {
    src new File(buildDir, 'ForBiggerFun.mp4')
    algorithm 'SHA-1'
    checksum '2ffd447ced3e0a04edcdd3975c4b638f16df2f7f'
}

task nativeBuild(type: Exec, dependsOn: compileJava) {
    workingDir './native'
    commandLine './build.sh'
}

processResources.dependsOn nativeBuild
test.dependsOn verifyFile